PHLI38*******************************
PHLI38 IDENTIFICATION DIVISION.
PHLI38*******************************
PHLI38*
PHLI38 PROGRAM-ID. DEPCHECK.
PHLI38*
PHLI38*******************************
PHLI38 ENVIRONMENT DIVISION.
PHLI38*******************************
PHLI38* 
PHLI38 INPUT-OUTPUT SECTION.
PHLI38 FILE-CONTROL.
PHLI38     SELECT DEPRETN         ASSIGN       TO EXTERNAL DEPRETN
PHLI38                            ORGANIZATION LINE SEQUENTIAL
PHLI38                            FILE STATUS  IS WS-DEPRETN-STATUS. 
LGTEST     SELECT NEWTOOLD        ASSIGN       TO EXTERNAL NEWTOOLD
LGTEST                            ORGANIZATION IS INDEXED
LGTEST                            ACCESS MODE  IS DYNAMIC
LGTEST                            RECORD KEY   IS NEWTOOLD-ACNO
LGTEST                            FILE STATUS  IS WS-NEWTOOLD-STATUS.
PHLI38*
PHLI38*******************************
PHLI38 DATA DIVISION.
PHLI38*******************************
PHLI38*
PHLI38 FILE SECTION.
PHLI38 FD  DEPRETN       RECORDING MODE IS F.
PHLI38 01  DEPRETN-RECORD.
PHLI38     03 DEPRETN-REC                   PIC X(400).
PHLI39 FD NEWTOOLD   RECORDING MODE IS F.
PHLI39 01 NEWTOOLD-REC.
PHLI39     03 NEWTOOLD-ACNO             PIC X(16).
PHLI39     03 NEWTOOLD-OLD              PIC X(25).
PHLI38*
PHLI38******************************* 
PHLI38 WORKING-STORAGE SECTION.
PHLI38*******************************
PHLI38*
PHLI38*******************************
PHLI38*    ORACLE CONNECT
PHLI38*******************************
PHLI38 77  ORACLE-ID  PIC X VALUE "/".
PHLI38*******************************
PHLI38*    FILE STATUS
PHLI38*******************************
PHLI38 01 WS-FILE-STS-AREA.
PHLI38    03 WS-DEPRETN-STATUS              PIC 9(02) VALUE 00.
PHLI38       88 WS-DEPRETN-SUCCESSFUL       VALUE 00.
PHLI38       88 WS-DEPRETN-EOF              VALUE 10.
PHLI38       88 WS-DEPRETN-ACCEPTABLE       VALUE 00 10 23.
PHLI38    03 WS-NEWTOOLD-STATUS              PIC 9(02) VALUE 00.
PHLI38       88 WS-NEWTOOLD-SUCCESSFUL       VALUE 00.
PHLI38       88 WS-NEWTOOLD-EOF              VALUE 10.
PHLI38       88 WS-NEWTOOLD-ACCEPTABLE       VALUE 00 10 23.
PHLI38*******************************
PHLI38*    FLAG AREA
PHLI38*******************************
PHLI38 01 WS-FLAG-AREA.
PHLI38     03  WS-PROD-FLAG                 PIC X(01) VALUE 'N'.
PHLI38         88  WS-PROD-FOUND            VALUE 'Y'.
PHLI38         88  WS-PROD-NOTFND           VALUE 'N'.
PHLI38     03 WS-SUB-AREA.
PHLI38        05 WS-SUB-1                   PIC 9(04) VALUE 0.
PHLI38        05 WS-SUB-2                   PIC 9(04) VALUE 0.
PHLI38     03 WS-LOOP-AREA.
PHLI38        05 WS-INVM-STOP               PIC 9 VALUE 0.
PHLI38     03 WS-COUNT-1                    PIC 9(08) VALUE 0.
PHLI38     03 WS-COUNT-2                    PIC 9(08) VALUE 0.
PHLI38******************************
PHLI38*    FILE FORMAT
PHLI38******************************
PHLI38 01 WS-RETRUN-DATE.
PHLI38     03 FILLER                PIC X(10) VALUE '切换日期：'.
PHLI38     03 WS-RET-CCYY                   PIC 9(04).
PHLI38     03 FILLER                        PIC X VALUE '/'.
PHLI38     03 WS-RET-MM                     PIC 9(02).
PHLI38     03 FILLER                        PIC X VALUE '/'.
PHLI38     03 WS-RET-DD                     PIC 9(02).
PHLI38 01 WS-HEAD.
PHLI38     03 FILLER        PIC X(12) VALUE SPACE.
PHLI38     03 WS-HEAD-1     PIC X(04) VALUE '账号'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 WS-HEAD-2     PIC X(08) VALUE '产品代码'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(22) VALUE SPACE.
PHLI38     03 WS-HEAD-3     PIC X(08) VALUE '产品名称'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 WS-HEAD-4     PIC X(06) VALUE '  属性'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(09) VALUE SPACE.
PHLI38     03 WS-HEAD-5     PIC X(08) VALUE '产品科目'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(11) VALUE SPACE.
PHLI38     03 WS-HEAD-6     PIC X(08) VALUE '账户余额'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(08) VALUE SPACE.
PHLI38     03 WS-HEAD-7     PIC X(09) VALUE '账户科目'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 WS-HEAD-8     PIC X(09) VALUE ' 账户利率'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(04) VALUE SPACE.
PHLI38     03 WS-HEAD-9     PIC X(06) VALUE '起息日'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(11) VALUE SPACE.
PHLI38     03 WS-HEAD-10    PIC X(08) VALUE '账户计提'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(11) VALUE SPACE.
PHLI38     03 WS-HEAD-11    PIC X(08) VALUE '核对计提'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 WS-HEAD-16    PIC X(06) VALUE '计提YN'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(11) VALUE SPACE.
PHLI38     03 WS-HEAD-17    PIC X(08) VALUE '计提差值'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(11) VALUE SPACE.
PHLI38     03 WS-HEAD-12    PIC X(08) VALUE '账户日息'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(11) VALUE SPACE.
PHLI38     03 WS-HEAD-13    PIC X(08) VALUE '核对日息'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 WS-HEAD-14    PIC X(06) VALUE '日息YN'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 FILLER        PIC X(11) VALUE SPACE.
PHLI38     03 WS-HEAD-15    PIC X(08) VALUE '日息差值'.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 WS-HEAD-16    PIC X VALUE ' '.
PHLI38     03 FILLER        PIC X VALUE '|'.
PHLI38     03 WS-HEAD-17    PIC X(06) VALUE ' ITYPE'.
PHLI38 01 WS-INVM-RETRUN.
PHLI38     03 WS-INVM-ACCT-NO               PIC 9(16).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-ACCT-TYPE             PIC X(04).
PHLI38     03 WS-INVM-INT-CAT               PIC X(04).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-DEP-DESCRIPT               PIC X(30).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 FILLER                        PIC X(5) VALUE SPACE.
PHLI38     03 WS-DEP-INT-METHOD             PIC X.
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-DEP-GL-CLASS-CODE          PIC X(17).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-CURR-BAL              PIC -Z(13)9.9(03).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-GL-CLASS-CODE         PIC X(17).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-VAR-INT-RATE          PIC -ZZ9.9(04).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-INT-FRM-DT.
PHLI38        05 WS-INVM-INT-FRM-CCYY       PIC 9(04).
PHLI38        05 FILLER                     PIC X VALUE '/'.
PHLI38        05 WS-INVM-INT-FRM-MM         PIC 9(02).
PHLI38        05 FILLER                     PIC X VALUE '/'.
PHLI38        05 WS-INVM-INT-FRM-DD         PIC 9(02).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-INT-AVAILABLE         PIC -Z(11)9.9(05).
PHLI38     03 WS-INVM-INT-SAVING  REDEFINES  
PHLI38                WS-INVM-INT-AVAILABLE PIC X(19).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-CALC-INT-ACCR              PIC -Z(11)9.9(05).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 FILLER                        PIC X(5) VALUE SPACE.
PHLI38     03 WS-CALC-ACCR-FLAG             PIC X.
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-CALC-ACCR                  PIC -Z(11)9.9(05).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-INT-INCR              PIC -Z(11)9.9(05).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-CALC-INT-INCR              PIC -Z(11)9.9(05).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 FILLER                        PIC X(5) VALUE SPACE.
PHLI38     03 WS-CALC-INCR-FLAG             PIC X.
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-CALC-INCR                  PIC -Z(11)9.9(05).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-MAT-IND               PIC X. 
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-ITYPE                 PIC -Z(11)9.9(05).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-INVM-INT-ITYPE             PIC -Z(11)9.9(05).
PHLI38     03 FILLER                        PIC X VALUE '|'.
PHLI38     03 WS-CALC-ITYPE                 PIC -Z(11)9.9(05).
PHLI38 01 WS-END.
PHLI38     03 FILLER        PIC X(16) VALUE '已核对账户数：'.
PHLI38     03 WS-END-COUNT  PIC 9(06).
PHLI38     03 FILLER        PIC X(10) VALUE '增量错误：'.
PHLI38     03 WS-END-COUNT2 PIC 9(06).
PHLI38     03 FILLER        PIC X(10) VALUE '计提错误：'.
PHLI38     03 WS-END-COUNT3 PIC 9(06).
PHLI38 01 WS-INVM.
PHLI38     03 WS-KEY-1                      PIC X(19).
PHLI38     03 WS-ACCT-TYPE                  PIC X(04).
PHLI38     03 WS-INT-CAT                    PIC X(04).
PHLI38     03 WS-CURR-BAL                   PIC S9(14)V9(03).
PHLI38     03 WS-GL-CLASS-CODE              PIC X(025).
PHLI38     03 WS-VAR-INT-RATE               PIC S9(03)V9(04).
PHLI38     03 WS-INT-FRM-DT                 PIC S9(9) BINARY.
PHLI38     03 WS-INT-TO-DT                  PIC S9(9) BINARY.
PHLI38     03 WS-ACCT-OPEN-DT               PIC S9(9) BINARY.
PHLI38     03 WS-INT-AVAILABLE              PIC S9(12)V9(05).
PHLI38     03 WS-INT-INCR                   PIC S9(12)V9(05).
PHLI38     03 WS-OD-VISA-AREA               PIC X(254).
PHLI38 01 WS-DEPP.
PHLI38     03 WS-DEPP-ACCT-TYPE             PIC X(04).
PHLI38     03 WS-DEPP-INT-CAT               PIC X(04).
PHLI38     03 WS-DEPP-DESCRIPT              PIC X(30).
PHLI38     03 WS-DEPP-INT-METHOD            PIC X(01).
PHLI38     03 WS-DEPP-CGL-CPNT-1-CR         PIC X(17).
PHLI38     03 WS-DEPP-CR-ID-DET             PIC X(08).
PHLI38 01  WS-PRODUCT-TABLES.
PHLI38     03  FILLER                       OCCURS 2000.
PHLI38         05  TAB-ACCT-TYPE            PIC 9(4). 
PHLI38         05  TAB-INT-CAT              PIC 9(4).
PHLI38         05  TAB-DESCRIPT             PIC X(32). 
PHLI38         05  TAB-INT-METHOD           PIC X(01). 
PHLI38         05  TAB-CGL-COMPONENT-1-CR   PIC 9(17).
PHLI38         05  TAB-CR-ID-DET            PIC X(08).
PHLI38 01 WS-DATE-BIN                       PIC S9(9) BINARY.
PHLI38 01 WS-RATE                           PIC 9(3)V9(4).
PHLI38 01 WS-DATE-DDMMCCYY.
PHLI38    03 WS-DATE-DD                     PIC 9(02).
PHLI38    03 WS-DATE-MM                     PIC 9(02).
PHLI38    03 WS-DATE-CCYY                   PIC 9(04).
PHLI38 01 WA-DATE-CCYYMMDD.
PHLI38    03 WA-DATE-CCYY                   PIC 9(04).
PHLI38    03 WA-DATE-MM                     PIC 9(02).
PHLI38    03 WA-DATE-DD                     PIC 9(02).
PHLI38 01 WS-CCYYMMDD-AREA.
PHLI38     03 WS-DAYS-NOW                   PIC 9(02).
PHLI38     03 WS-DAYS-LAST                  PIC 9(02).
PHLI38     03 WS-TODAY-BIN                  PIC S9(9) BINARY.
PHLI38     03 WS-TODAY-CCYYMMDD.
PHLI38         05 WS-TODAY-CCYYMM.
PHLI38            07 WS-TODAY-CCYY          PIC 9(04).
PHLI38            07 WS-TODAY-MM            PIC 9(02).
PHLI38         05 WS-TODAY-DD               PIC 9(02).
PHLI38     03 WS-LAST-CCYYMMDD.
PHLI38         05 WS-LAST-CCYYMM.
PHLI38            07 WS-LAST-CCYY           PIC 9(04).
PHLI38            07 WS-LAST-MM             PIC 9(02).
PHLI38         05 WS-LAST-DD                PIC 9(02).
PHLI38     03 WS-OPEN-CCYYMMDD.
PHLI38         05 WS-OPEN-CCYYMM.
PHLI38            07 WS-OPEN-CCYY           PIC 9(04).
PHLI38            07 WS-OPEN-MM             PIC 9(02).
PHLI38         05 WS-OPEN-DD                PIC 9(02).
PHLI38     03 WS-INTFROM-CCYYMMDD.
PHLI38         05 WS-INTFROM-CCYYMM.
PHLI38            07 WS-INTFROM-CCYY        PIC 9(04).
PHLI38            07 WS-INTFROM-MM          PIC 9(02).
PHLI38         05 WS-INTFROM-DD             PIC 9(02).
PHLI38     03 WS-INT-TO-CCYYMMDD.
PHLI38         05 WS-INT-TO-CCYYMM.
PHLI38            07 WS-INT-TO-CCYY         PIC 9(04).
PHLI38            07 WS-INT-TO-MM           PIC 9(02).
PHLI38         05 WS-INT-TO-DD              PIC 9(02).
PHLI38     03 WS-MAT-CCYYMMDD.
PHLI38         05 WS-MAT-CCYYMM.
PHLI38            07 WS-MAT-CCYY            PIC 9(04).
PHLI38            07 WS-MAT-MM              PIC 9(02).
PHLI38         05 WS-MAT-DD                 PIC 9(02).
PHLI38 01 WS-WORKING-AREA.
PHLI38     03 WS-WORKING-RATE               PIC S9(03)V9(04).
PHLI38     03 WS-WORKING-ACCR               PIC S9(12)V9(05).
PHLI38     03 WS-WORKING-INCR               PIC S9(12)V9(05).
PHLI38     03 WS-WORKING-ITYPE              PIC S9(12)V9(05).
PHLI38     03 WS-WORK-CALC-RATE             PIC S9(03)V9(04).
PHLI38     03 WS-WORK-CALC-ACCR             PIC S9(12)V9(05).
PHLI38     03 WS-WORK-CALC-ITYPE            PIC S9(12)V9(05).
PHLI38     03 WS-WORK-CALC-INCR             PIC S9(12)V9(05).
PHLI38     03 W100-OD-AREA.
PHLI38       05  FILLER                          PIC X(46).
PHLI38       05  W100-RATE-IND                   PIC X(01).
PHLI38       05  FILLER                          PIC X(02).
PHLI38       05  W100-ITYPE-INT-ACCR             PIC S9(14)V999 COMP.
PHLI38       05  FILLER                          PIC X(183).  
PHLI38 COPY UT0020CA IN LIBRYMIS.
PHLI38 COPY UT0030CA IN LIBRYMIS.
PHLI38 COPY UT1999CA IN LIBRYMIS.
PHLI38 01  UT7500CA-CALL-AREA.
PHLI38     COPY UT7500CA IN LIBRYTRE. 
PHLI38*
PHLI38      EXEC SQL BEGIN DECLARE SECTION END-EXEC.
PHLI38 01 D-DTEP-MON                        PIC X(06).
PHLI38 01 D-INVM.
PHLI38     03 D-KEY-1                       PIC X(19).
PHLI38     03 D-ACCT-TYPE                   PIC X(04).
PHLI38     03 D-INT-CAT                     PIC X(04).
PHLI38     03 D-CURR-BAL                    PIC S9(14)V9(03) COMP-3.
PHLI38     03 D-GL-CLASS-CODE               PIC X(025).
PHLI38     03 D-VAR-INT-RATE                PIC S9(03)V9(04) COMP-3.
PHLI38     03 D-INT-FRM-DT                  PIC S9(09) COMP.
PHLI38     03 D-INT-TO-DT                   PIC S9(09) COMP.
PHLI38     03 D-ACCT-OPEN-DT                PIC S9(09) COMP.
PHLI38     03 D-INT-AVAILABLE               PIC S9(12)V9(05) COMP-3.
PHLI38     03 D-INT-INCR                    PIC S9(12)V9(05) COMP-3.
PHLI38     03 D-OD-VISA-AREA                PIC X(254).
PHLI38 01 P-DEPP.
PHLI38     03 P-ACCT-TYPE                   PIC X(04).
PHLI38     03 P-INT-CAT                     PIC X(04).
PHLI38     03 P-DESCRIPT                    PIC X(30).
PHLI38     03 P-INT-METHOD                  PIC X(01).
PHLI38     03 P-CGL-CPNT-1-CR               PIC X(17).
PHLI38     03 P-CR-ID-DET                   PIC X(08).
PHLI38 01 D-XREF.
PHLI38     03 D-EXTN-REF-NO                 PIC X(20).
PHLI38     03 D-INTN-REF-NO                 PIC X(16).
PHLI38 01 D-DTEP.
PHLI38     03 NO-DYS-MTH                    PIC 9(02).
PHLI38      EXEC SQL END DECLARE SECTION END-EXEC.
PHLI38      EXEC SQL
PHLI38         INCLUDE SQLCA
PHLI38      END-EXEC.
PHLI38********************************
PHLI38 PROCEDURE DIVISION.
PHLI38********************************
PHLI38*
PHLI38 A000-MAIN-LINE.
PHLI38      OPEN  OUTPUT DEPRETN.
PHLI38      MOVE 1                  TO UT1999CA-CALL-CODE.         
PHLI38      CALL "UT1999" USING UT1999CA-CALL-AREA.                        
PHLI38      MOVE UT1999CA-CCYYMMDD  TO WS-TODAY-CCYYMMDD
PHLI38                                 WS-LAST-CCYYMMDD
PHLI38                                 WA-DATE-CCYYMMDD
PHLI38      PERFORM C200-DATE-BIN THRU C209-EXIT.
PHLI38      MOVE WS-DATE-BIN TO WS-TODAY-BIN
PHLI38      SUBTRACT 1 FROM WS-LAST-MM
PHLI38      IF WS-LAST-MM = 0
PHLI38         MOVE 12 TO WS-LAST-MM
PHLI38         SUBTRACT 1 FROM WS-LAST-CCYY
PHLI38      END-IF.
PHLI38     INITIALIZE DEPRETN-RECORD.
PHLI38     MOVE WS-HEAD            TO DEPRETN-REC.
PHLI38     WRITE DEPRETN-RECORD.
PHLI38      EXEC SQL
PHLI38         CONNECT :ORACLE-ID
PHLI38      END-EXEC.
PHLI38      MOVE WS-TODAY-CCYYMM TO D-DTEP-MON
PHLI38      EXEC SQL
PHLI38          SELECT 
PHLI38          NO_DYS_MTH
PHLI38          INTO :WS-DAYS-NOW
PHLI38          FROM DTEP
PHLI38          WHERE DTEP_ID = '00'
PHLI38          AND DATE_YYYYMM = :D-DTEP-MON
PHLI38      END-EXEC.
PHLI38      MOVE WS-LAST-CCYYMM TO D-DTEP-MON
PHLI38      EXEC SQL
PHLI38          SELECT 
PHLI38          NO_DYS_MTH
PHLI38          INTO :WS-DAYS-LAST
PHLI38          FROM DTEP
PHLI38          WHERE DTEP_ID = '00'
PHLI38          AND DATE_YYYYMM = :D-DTEP-MON
PHLI38      END-EXEC.
PHLI38      PERFORM A100-READ-DEPP THRU A109-EXIT.
PHLI38      PERFORM B100-READ-INVM THRU B109-EXIT.
PHLI38      CLOSE DEPRETN.
PHLI38 A099-EXIT.
PHLI38     STOP RUN.
PHLI38     
PHLI38 A100-READ-DEPP.
PHLI38      EXEC SQL DECLARE D_PRODUCT CURSOR FOR
PHLI38          SELECT 
PHLI38          TYPE,
PHLI38          INT_CAT,
PHLI38          DESCRIPT,
PHLI38          INT_METHOD,
PHLI38          CGL_CPNT_1_CR,
PHLI38          CR_ID_DET
PHLI38          FROM DEPP
PHLI38      END-EXEC.
PHLI38      MOVE 0 TO WS-SUB-1
PHLI38      EXEC SQL OPEN D_PRODUCT
PHLI38      END-EXEC.
PHLI38      PERFORM A200-LOAD-PRODUCT THRU A209-EXIT
PHLI38                          UNTIL SQLCODE NOT = 0
PHLI38                             OR WS-SUB-1 = 2000.
PHLI38      EXEC SQL CLOSE D_PRODUCT
PHLI38      END-EXEC.
PHLI38 A109-EXIT.
PHLI38     EXIT.
PHLI38 A200-LOAD-PRODUCT.
PHLI38      EXEC SQL
PHLI38       FETCH D_PRODUCT
PHLI38       INTO :P-ACCT-TYPE,
PHLI38            :P-INT-CAT,
PHLI38            :P-DESCRIPT,
PHLI38            :P-INT-METHOD,
PHLI38            :P-CGL-CPNT-1-CR,
PHLI38            :P-CR-ID-DET
PHLI38      END-EXEC.
PHLI38     IF SQLCODE = 0
PHLI38        ADD 1 TO WS-SUB-1
PHLI38        MOVE SPACE        TO TAB-DESCRIPT(WS-SUB-1)
PHLI38                             TAB-INT-METHOD(WS-SUB-1)
PHLI38        MOVE ZEROS        TO TAB-ACCT-TYPE(WS-SUB-1)
PHLI38                             TAB-INT-CAT(WS-SUB-1)
PHLI38                             TAB-CGL-COMPONENT-1-CR(WS-SUB-1)
PHLI38        MOVE P-ACCT-TYPE  TO TAB-ACCT-TYPE(WS-SUB-1)
PHLI38        MOVE P-INT-CAT    TO TAB-INT-CAT(WS-SUB-1)
PHLI38        MOVE P-DESCRIPT   TO TAB-DESCRIPT(WS-SUB-1)
PHLI38        MOVE P-INT-METHOD TO TAB-INT-METHOD(WS-SUB-1)
PHLI38        MOVE P-CGL-CPNT-1-CR TO 
PHLI38               TAB-CGL-COMPONENT-1-CR(WS-SUB-1)
PHLI38        MOVE P-CR-ID-DET TO TAB-CR-ID-DET(WS-SUB-1)
PHLI38     END-IF.
PHLI38 A209-EXIT.
PHLI38     EXIT.  
PHLI38
PHLI38 A300-GET-PRODUCT.
PHLI38     SET WS-PROD-NOTFND                  TO TRUE.
PHLI38     MOVE 0 TO WS-RATE
PHLI38     PERFORM VARYING WS-SUB-2 FROM 1 BY 1
PHLI38               UNTIL WS-SUB-2 > WS-SUB-1 
PHLI38                  OR WS-PROD-FOUND
PHLI38        IF WS-DEPP-ACCT-TYPE = TAB-ACCT-TYPE(WS-SUB-2) AND
PHLI38           WS-DEPP-INT-CAT   = TAB-INT-CAT(WS-SUB-2)
PHLI38           SET WS-PROD-FOUND TO TRUE
PHLI38           MOVE TAB-DESCRIPT(WS-SUB-2)  TO WS-DEPP-DESCRIPT
PHLI38           MOVE TAB-INT-METHOD(WS-SUB-2) TO WS-DEPP-INT-METHOD
PHLI38           MOVE TAB-CGL-COMPONENT-1-CR(WS-SUB-2) TO 
PHLI38                                   WS-DEPP-CGL-CPNT-1-CR
PHLI38           MOVE TAB-CR-ID-DET(WS-SUB-2)  TO WS-DEPP-CR-ID-DET
PHLI38           EVALUATE WS-DEPP-CR-ID-DET
PHLI38              WHEN 'DR01R001'
PHLI38                  MOVE 0.385 TO WS-RATE
PHLI38              WHEN 'DR01R002'
PHLI38                  MOVE 2.86 TO WS-RATE
PHLI38              WHEN 'DR01R003'
PHLI38                  MOVE 3.08 TO WS-RATE
PHLI38              WHEN 'DR01R004'
PHLI38                  MOVE 3.3 TO WS-RATE
PHLI38              WHEN 'DR01R005'
PHLI38                  MOVE 4.125 TO WS-RATE
PHLI38              WHEN 'DR01R006'
PHLI38                  MOVE 4.675 TO WS-RATE
PHLI38              WHEN 'DR01R007'
PHLI38                  MOVE 5.225 TO WS-RATE
PHLI38              WHEN 'DR01R008'
PHLI38                  MOVE 2.86 TO WS-RATE
PHLI38              WHEN 'DR01R009'
PHLI38                  MOVE 3.08 TO WS-RATE
PHLI38              WHEN 'DR01R010'
PHLI38                  MOVE 3.3 TO WS-RATE
PHLI38              WHEN 'DR01R011'
PHLI38                  MOVE 1.265 TO WS-RATE
PHLI38              WHEN 'DR01R012'
PHLI38                  MOVE 0.88 TO WS-RATE
PHLI38              WHEN 'DR01R013'
PHLI38                  MOVE 1.485 TO WS-RATE
PHLI38              WHEN 'DR01R015'
PHLI38                  MOVE 0.35 TO WS-RATE
PHLI38              WHEN 'DR01R016'
PHLI38                  MOVE 2.6 TO WS-RATE
PHLI38              WHEN 'DR01R017'
PHLI38                  MOVE 2.8 TO WS-RATE
PHLI38              WHEN 'DR01R018'
PHLI38                  MOVE 3 TO WS-RATE
PHLI38              WHEN 'DR01R019'
PHLI38                  MOVE 3.75 TO WS-RATE
PHLI38              WHEN 'DR01R020'
PHLI38                  MOVE 4.25 TO WS-RATE
PHLI38              WHEN 'DR01R021'
PHLI38                  MOVE 4.75 TO WS-RATE
PHLI38              WHEN OTHER
PHLI38                  MOVE 0 TO WS-RATE
PHLI38           END-EVALUATE
PHLI38        END-IF
PHLI38     END-PERFORM.
PHLI38     IF WS-PROD-NOTFND
PHLI38        MOVE ZEROS  TO WS-DEPP-CGL-CPNT-1-CR
PHLI38        MOVE '0'    TO WS-DEPP-INT-METHOD
PHLI38        MOVE SPACE  TO WS-DEPP-DESCRIPT
PHLI38     END-IF.
PHLI38     MOVE WS-DEPP-DESCRIPT   TO WS-DEP-DESCRIPT
PHLI38     MOVE WS-DEPP-INT-METHOD TO WS-DEP-INT-METHOD
PHLI38     MOVE WS-DEPP-CGL-CPNT-1-CR TO WS-DEP-GL-CLASS-CODE.
PHLI38 A309-EXIT.
PHLI38     EXIT.
PHLI38 B100-READ-INVM.
PHLI38      EXEC SQL DECLARE D_ACCOUNT CURSOR FOR
PHLI38       SELECT 
PHLI38       KEY_1,
PHLI38       ACCT_TYPE,
PHLI38       INT_CAT,
PHLI38       CURR_BAL,
PHLI38       GL_CLASS_CODE,
PHLI38       VAR_INT_RATE,
PHLI38       INT_FRM_DT,
PHLI38       INT_TO_DT,
PHLI38       ACCT_OPEN_DT,
PHLI38       INT_AVAILABLE,
PHLI38       INT_INCR,
PHLI38       OD_VISA_AREA
PHLI38       FROM INVM
PHLI38      END-EXEC.
PHLI38      EXEC SQL OPEN D_ACCOUNT
PHLI38      END-EXEC.
PHLI38      PERFORM B200-FETCH-INVM THRU B209-EXIT
PHLI38                          UNTIL SQLCODE NOT = 0
PHLI38                             OR WS-INVM-STOP NOT = 0
PHLI38      EXEC SQL CLOSE D_ACCOUNT
PHLI38      END-EXEC.
PHLI38 B109-EXIT.
PHLI38     EXIT.
PHLI38
PHLI38 B200-FETCH-INVM.
PHLI38      EXEC SQL
PHLI38       FETCH D_ACCOUNT
PHLI38       INTO :D-INVM
PHLI38      END-EXEC.
PHLI38      IF SQLCODE = 0
PHLI38        IF D-KEY-1 = WS-KEY-1
PHLI38           MOVE 1 TO WS-INVM-STOP 
PHLI38           GO TO B209-EXIT
PHLI38        END-IF
PHLI38        ADD 1     TO WS-COUNT-1
PHLI38        IF WS-COUNT-1 = 10000
PHLI38           ADD WS-COUNT-1 TO WS-COUNT-2
PHLI38           DISPLAY WS-COUNT-2
PHLI38           MOVE 1 TO WS-COUNT-1
PHLI38        END-IF
PHLI38        INITIALIZE WS-INVM-RETRUN
PHLI38                   DEPRETN-RECORD
PHLI38        MOVE D-KEY-1         TO WS-KEY-1
PHLI38        MOVE WS-KEY-1(4:16)  TO WS-INVM-ACCT-NO
PHLI38        MOVE D-ACCT-TYPE     TO WS-ACCT-TYPE
PHLI38                                WS-DEPP-ACCT-TYPE
PHLI38                                WS-INVM-ACCT-TYPE
PHLI38        MOVE D-INT-CAT       TO WS-INT-CAT
PHLI38                                WS-DEPP-INT-CAT
PHLI38                                WS-INVM-INT-CAT
PHLI38        MOVE D-CURR-BAL      TO WS-CURR-BAL
PHLI38                                WS-INVM-CURR-BAL
PHLI38        MOVE D-GL-CLASS-CODE TO WS-GL-CLASS-CODE
PHLI38        MOVE WS-GL-CLASS-CODE(9:10) TO WS-INVM-GL-CLASS-CODE
PHLI38        MOVE D-VAR-INT-RATE  TO WS-VAR-INT-RATE
PHLI38                                WS-WORKING-RATE
PHLI38                                WS-INVM-VAR-INT-RATE
PHLI38        MOVE D-INT-FRM-DT    TO WS-INT-FRM-DT
PHLI38                                WS-DATE-BIN
PHLI38        PERFORM C100-DATE-DDMMCCYY THRU C109-EXIT
PHLI38        MOVE WA-DATE-CCYYMMDD TO WS-INTFROM-CCYYMMDD
PHLI38        SUBTRACT 1 FROM WS-DATE-BIN
PHLI38        PERFORM C100-DATE-DDMMCCYY THRU C109-EXIT
PHLI38        MOVE WA-DATE-CCYYMMDD TO WS-MAT-CCYYMMDD
PHLI38        MOVE WS-INTFROM-CCYY  TO WS-INVM-INT-FRM-CCYY
PHLI38        MOVE WS-INTFROM-MM    TO WS-INVM-INT-FRM-MM
PHLI38        MOVE WS-INTFROM-DD    TO WS-INVM-INT-FRM-DD
PHLI38        MOVE D-INT-TO-DT     TO WS-INT-TO-DT
PHLI38                                WS-DATE-BIN
PHLI38        PERFORM C100-DATE-DDMMCCYY THRU C109-EXIT
PHLI38        MOVE WA-DATE-CCYYMMDD TO WS-INT-TO-CCYYMMDD
PHLI38        MOVE D-ACCT-OPEN-DT  TO WS-ACCT-OPEN-DT
PHLI38                                WS-DATE-BIN
PHLI38        PERFORM C100-DATE-DDMMCCYY THRU C109-EXIT
PHLI38        MOVE WA-DATE-CCYYMMDD TO WS-OPEN-CCYYMMDD
PHLI38        IF WS-OPEN-MM = 2 AND WS-OPEN-DD = 29
PHLI38          AND WS-TODAY-MM = 3
PHLI38          MOVE 29 TO WS-DAYS-LAST
PHLI38        END-IF
PHLI38        MOVE D-INT-AVAILABLE TO WS-INT-AVAILABLE
PHLI38                                WS-INVM-INT-AVAILABLE
PHLI38        MOVE D-INT-INCR      TO WS-INT-INCR
PHLI38                                WS-INVM-INT-INCR
PHLI38        MOVE D-OD-VISA-AREA  TO W100-OD-AREA
PHLI38        MOVE W100-RATE-IND   TO WS-INVM-MAT-IND
PHLI38        MOVE W100-ITYPE-INT-ACCR TO WS-INVM-ITYPE
PHLI38        MOVE ZEROS           TO WS-DEPP-CGL-CPNT-1-CR
PHLI38        MOVE SPACE           TO WS-DEP-DESCRIPT
PHLI38                                WS-DEPP-DESCRIPT
PHLI38        PERFORM A300-GET-PRODUCT THRU A309-EXIT
PHLI38        PERFORM B300-CALC-INT    THRU B309-EXIT
PHLI38     END-IF.
PHLI38 B209-EXIT.
PHLI38     EXIT.
PHLI38 B300-CALC-INT.
PHLI38     EVALUATE WS-DEPP-INT-METHOD
PHLI38        WHEN '1'
PHLI38          PERFORM E200-SAVING THRU E209-EXIT
PHLI38        WHEN '9'
PHLI38          PERFORM E300-TERM THRU E309-EXIT
PHLI38        WHEN 'Y'
PHLI38          PERFORM E400-TERM-SAVING THRU E409-EXIT
PHLI38     END-EVALUATE.
PHLI38 B309-EXIT.
PHLI38     EXIT.
PHLI38 C100-DATE-DDMMCCYY.
PHLI38     MOVE WS-DATE-BIN          TO UT0020-BINARY-DATE.
PHLI38     MOVE 1                    TO UT0020-FUNCTION.
PHLI38     CALL "UT0020" USING UT0020-FUNCTION
PHLI38                         UT0020-DATE-AREA
PHLI38                         UT0020-BINARY-DATE.
PHLI38     MOVE UT0020-DATE-DDMMCCYY TO WS-DATE-DDMMCCYY.
PHLI38     MOVE WS-DATE-DD           TO WA-DATE-DD.
PHLI38     MOVE WS-DATE-MM           TO WA-DATE-MM.
PHLI38     MOVE WS-DATE-CCYY         TO WA-DATE-CCYY.
PHLI38 C109-EXIT.
PHLI38     EXIT.
PHLI38 C200-DATE-BIN.
PHLI38     MOVE WA-DATE-CCYYMMDD TO UT0030-DATE-CCYYMMDD
PHLI38     MOVE 2                TO UT0030-FUNCTION
PHLI38     CALL "UT0030" USING UT0030-FUNCTION
PHLI38                         UT0030-DATE-AREA
PHLI38                         UT0030-BINARY-DATE
PHLI38     MOVE UT0030-BINARY-DATE TO WS-DATE-BIN.
PHLI38 C209-EXIT.
PHLI38     EXIT.
PHLI38 E200-SAVING.
PHLI38     MOVE WS-RATE            TO WS-WORKING-RATE
PHLI38                              WS-INVM-VAR-INT-RATE
PHLI38     IF WS-VAR-INT-RATE NOT = 99.9999
PHLI38        MOVE WS-VAR-INT-RATE TO WS-WORKING-RATE
PHLI38                              WS-INVM-VAR-INT-RATE
PHLI38     END-IF
PHLI38     MOVE 'SAVING ACCT'    TO WS-INVM-INT-SAVING
PHLI38     COMPUTE WS-WORKING-INCR = WS-CURR-BAL * WS-WORKING-RATE
PHLI38                            / 36000
PHLI38     MOVE 0                TO WS-CALC-INT-ACCR
PHLI38                              WS-CALC-ACCR
PHLI38     MOVE 'Y'              TO WS-CALC-ACCR-FLAG 
PHLI38     MOVE WS-WORKING-INCR  TO WS-CALC-INT-INCR
PHLI38     COMPUTE WS-WORK-CALC-INCR =  WS-INT-INCR - WS-WORKING-INCR
PHLI38     MOVE WS-WORK-CALC-INCR TO WS-CALC-INCR
PHLI38     IF WS-WORK-CALC-INCR > -0.00002
PHLI38       AND WS-WORK-CALC-INCR < 0.00002
PHLI38       MOVE 'Y'            TO WS-CALC-INCR-FLAG
PHLI38     ELSE
PHLI38       MOVE 'N'            TO WS-CALC-INCR-FLAG
PHLI38     END-IF.
PHLI38     MOVE WS-INVM-RETRUN   TO DEPRETN-REC
PHLI38     IF WS-CALC-INCR-FLAG = 'N' 
PHLI38       OR WS-CALC-ACCR-FLAG = 'N' 
PHLI38         WRITE DEPRETN-RECORD
PHLI38     END-IF.
PHLI38 E209-EXIT.
PHLI38     EXIT.
PHLI38 E300-TERM.
PHLI38     IF W100-RATE-IND = 'M'
PHLI38        COMPUTE WS-WORKING-INCR  ROUNDED = 
                        (  WS-CURR-BAL *  0.385 )
PHLI38                                / 36000
PHLI38        COMPUTE WS-WORKING-ACCR = WS-WORKING-INCR *
PHLI38                ( WS-TODAY-BIN - WS-INT-FRM-DT )
PHLI38        COMPUTE WS-WORK-CALC-INCR ROUNDED = 
PHLI38                         WS-INT-INCR - WS-WORKING-INCR
PHLI38        COMPUTE WS-WORK-CALC-ACCR = 
PHLI38                 WS-INT-AVAILABLE - WS-WORKING-ACCR
PHLI38        MOVE WS-WORK-CALC-ACCR TO WS-CALC-ACCR
PHLI38        MOVE WS-WORKING-ACCR   TO WS-CALC-INT-ACCR
PHLI38        IF WS-WORK-CALC-ACCR > -0.001
PHLI38          AND WS-WORK-CALC-ACCR < 0.001
PHLI38          MOVE 'Y'            TO WS-CALC-ACCR-FLAG
PHLI38        ELSE
PHLI38          MOVE 'N'            TO WS-CALC-ACCR-FLAG
PHLI38        END-IF
PHLI38        MOVE WS-WORK-CALC-INCR TO WS-CALC-INCR
PHLI38        MOVE WS-WORKING-INCR   TO WS-CALC-INT-INCR
PHLI38        IF WS-WORK-CALC-INCR > -0.00002
PHLI38          AND WS-WORK-CALC-INCR < 0.00002
PHLI38          MOVE 'Y'            TO WS-CALC-INCR-FLAG
PHLI38        ELSE
PHLI38          MOVE 'N'            TO WS-CALC-INCR-FLAG
PHLI38        END-IF
PHLI38     ELSE
PHLI38        INITIALIZE UT7500CA-CALL-AREA
PHLI38        MOVE WS-INTFROM-CCYYMMDD TO UT7500CA-DATE1-CCYYMMDD
PHLI38        MOVE WS-TODAY-CCYYMMDD  TO UT7500CA-DATE2-CCYYMMDD
PHLI38        MOVE  1               TO UT7500CA-CALL-FUNCTION
PHLI38        MOVE  "M"             TO UT7500CA-CALL-TYPE
PHLI38        CALL  "UT7500"     USING UT7500CA-CALL-AREA
PHLI38        COMPUTE WS-WORKING-ACCR = ( ( WS-CURR-BAL 
PHLI38                       * WS-WORKING-RATE / 1200 )
PHLI38                       * UT7500CA-NO-MONTHS ) 
PHLI38        IF UT7500CA-NO-DAYS < WS-TODAY-DD
PHLI38           COMPUTE WS-WORKING-INCR ROUNDED = ( WS-CURR-BAL * 
PHLI38                   WS-WORKING-RATE / 1200 ) / WS-DAYS-NOW
PHLI38        ELSE
PHLI38        IF ( WS-DAYS-NOW = 30 AND WS-INTFROM-DD = 31 )
PHLI38         OR ( WS-DAYS-NOW = 29 AND WS-INTFROM-DD = ( 31 OR 30 ) )
PHLI38         OR ( WS-DAYS-NOW = 28 AND
PHLI38                   WS-INTFROM-DD = ( 31 OR 30 OR 29) )
PHLI38        COMPUTE WS-WORKING-INCR ROUNDED = (  WS-CURR-BAL * 
PHLI38                WS-WORKING-RATE / 1200 ) / WS-DAYS-NOW
PHLI38        ELSE
PHLI38           COMPUTE WS-WORKING-INCR ROUNDED = (  WS-CURR-BAL * 
PHLI38                   WS-WORKING-RATE / 1200 ) / WS-DAYS-LAST
PHLI38        END-IF
PHLI38        END-IF
PHLI38        COMPUTE WS-WORKING-ACCR = WS-WORKING-ACCR +
PHLI38                   ( WS-WORKING-INCR * UT7500CA-NO-DAYS )
PHLI38        COMPUTE WS-WORKING-ITYPE = WS-WORKING-INCR *
PHLI38                ( WS-TODAY-BIN - WS-INT-FRM-DT )
PHLI38        COMPUTE WS-WORK-CALC-INCR ROUNDED = 
PHLI38                         WS-INT-INCR - WS-WORKING-INCR
PHLI38        COMPUTE WS-WORK-CALC-ACCR = 
PHLI38                 WS-INT-AVAILABLE - WS-WORKING-ACCR
PHLI38        MOVE WS-WORK-CALC-ACCR TO WS-CALC-ACCR
PHLI38        MOVE WS-WORKING-ACCR   TO WS-CALC-INT-ACCR
PHLI38        IF WS-WORK-CALC-ACCR > -0.01
PHLI38          AND WS-WORK-CALC-ACCR < 0.01
PHLI38          MOVE 'Y'            TO WS-CALC-ACCR-FLAG
PHLI38        ELSE
PHLI38          MOVE 'N'            TO WS-CALC-ACCR-FLAG
PHLI38        END-IF
PHLI38        MOVE WS-WORK-CALC-INCR TO WS-CALC-INCR
PHLI38        MOVE WS-WORKING-INCR   TO WS-CALC-INT-INCR
PHLI38        IF WS-WORK-CALC-INCR > -0.00002
PHLI38          AND WS-WORK-CALC-INCR < 0.00002
PHLI38          MOVE 'Y'            TO WS-CALC-INCR-FLAG
PHLI38        ELSE
PHLI38          MOVE 'N'            TO WS-CALC-INCR-FLAG
PHLI38        END-IF
PHLI38     END-IF.
PHLI38     MOVE WS-INVM-RETRUN   TO DEPRETN-REC
PHLI38     IF WS-CALC-INCR-FLAG = 'N' 
PHLI38         WRITE DEPRETN-RECORD
PHLI38     ELSE
PHLI38        IF WS-CALC-ACCR-FLAG = 'N'
PHLI38          AND WS-ACCT-TYPE NOT = '2231' AND '2233'
PHLI38          IF WS-ACCT-TYPE = '2230' AND W100-RATE-IND = 'M'
PHLI38             CONTINUE
PHLI38          ELSE
PHLI38             WRITE DEPRETN-RECORD
PHLI38          END-IF
PHLI38        END-IF
PHLI38     END-IF.
PHLI38 E309-EXIT.
PHLI38     EXIT.
PHLI38 E400-TERM-SAVING.
PHLI38     INITIALIZE UT7500CA-CALL-AREA
PHLI38     MOVE WS-INTFROM-CCYYMMDD TO UT7500CA-DATE1-CCYYMMDD
PHLI38     MOVE WS-TODAY-CCYYMMDD  TO UT7500CA-DATE2-CCYYMMDD
PHLI38     MOVE  1               TO UT7500CA-CALL-FUNCTION
PHLI38     MOVE  "M"             TO UT7500CA-CALL-TYPE
PHLI38     CALL  "UT7500"     USING UT7500CA-CALL-AREA
PHLI38     COMPUTE WS-WORKING-ACCR = ( ( WS-CURR-BAL 
PHLI38                    * WS-WORKING-RATE / 1200 )
PHLI38                    * UT7500CA-NO-MONTHS ) 
PHLI38     IF UT7500CA-NO-DAYS < WS-TODAY-DD
PHLI38        COMPUTE WS-WORKING-INCR ROUNDED = ( WS-CURR-BAL * 
PHLI38                WS-WORKING-RATE / 1200 ) / WS-DAYS-NOW
PHLI38     ELSE
PHLI38        IF ( WS-DAYS-NOW = 30 AND WS-INTFROM-DD = 31 )
PHLI38         OR ( WS-DAYS-NOW = 29 AND WS-INTFROM-DD = ( 31 OR 30 ) )
PHLI38         OR ( WS-DAYS-NOW = 28 AND
PHLI38                   WS-INTFROM-DD = ( 31 OR 30 OR 29) )
PHLI38        COMPUTE WS-WORKING-INCR ROUNDED = (  WS-CURR-BAL * 
PHLI38                WS-WORKING-RATE / 1200 ) / WS-DAYS-NOW
PHLI38        ELSE
PHLI38        COMPUTE WS-WORKING-INCR ROUNDED = (  WS-CURR-BAL * 
PHLI38                WS-WORKING-RATE / 1200 ) / WS-DAYS-LAST
PHLI38        END-IF
PHLI38     END-IF
PHLI38     IF WS-ACCT-TYPE = '2223'
PHLI38        COMPUTE WS-WORKING-INCR ROUNDED = WS-CURR-BAL * 
PHLI38                WS-WORKING-RATE / 36000
PHLI38     END-IF
PHLI38     COMPUTE WS-WORKING-ACCR = WS-WORKING-ACCR +
PHLI38                ( WS-WORKING-INCR * UT7500CA-NO-DAYS )
PHLI38     COMPUTE WS-WORKING-ITYPE = WS-WORKING-INCR *
PHLI38             ( WS-TODAY-BIN - WS-INT-FRM-DT )
PHLI38     COMPUTE WS-WORK-CALC-INCR ROUNDED = 
PHLI38                      WS-INT-INCR - WS-WORKING-INCR
PHLI38     COMPUTE WS-WORK-CALC-ACCR = 
PHLI38              WS-INT-AVAILABLE - WS-WORKING-ACCR
PHLI38     MOVE WS-WORK-CALC-ACCR TO WS-CALC-ACCR
PHLI38     MOVE WS-WORKING-ACCR   TO WS-CALC-INT-ACCR
PHLI38     IF WS-WORK-CALC-ACCR > -0.01
PHLI38       AND WS-WORK-CALC-ACCR < 0.01
PHLI38       MOVE 'Y'            TO WS-CALC-ACCR-FLAG
PHLI38     ELSE
PHLI38       MOVE 'N'            TO WS-CALC-ACCR-FLAG
PHLI38     END-IF.
PHLI38     MOVE WS-WORK-CALC-INCR TO WS-CALC-INCR
PHLI38     MOVE WS-WORKING-INCR   TO WS-CALC-INT-INCR
PHLI38     IF WS-WORK-CALC-INCR > -0.00002
PHLI38       AND WS-WORK-CALC-INCR < 0.00002
PHLI38       MOVE 'Y'            TO WS-CALC-INCR-FLAG
PHLI38     ELSE
PHLI38       MOVE 'N'            TO WS-CALC-INCR-FLAG
PHLI38     END-IF.
PHLI38     MOVE WS-INVM-RETRUN   TO DEPRETN-REC
PHLI38     IF WS-CALC-INCR-FLAG = 'N' 
PHLI38       OR WS-CALC-ACCR-FLAG = 'N' 
PHLI38         WRITE DEPRETN-RECORD
PHLI38     END-IF.
PHLI38 E409-EXIT.
PHLI38     EXIT.